#Bat批处理文件中用于计时的一个小工具
[stackoverflow](https://stackoverflow.com/questions/43754374/execute-multiple-batch-files-concurrently-and-monitor-if-their-process-is-comple)  
[多进程](多进程.bat)
```commandline
@echo off
    setlocal enableextensions disabledelayedexpansion

    for %%t in ("%temp%\%~nx0.%random%%random%%random%.tmp") do (

        echo Starting subprocesses
        9> "%%~ft" (
            start "" cmd /c subprocess1.bat ^>log1.log
            start "" cmd /c subprocess2.bat ^>log2.log
            start "" cmd /c subprocess3.bat ^>log3.log
            start "" cmd /c subprocess4.bat ^>log4.log
            start "" cmd /c subprocess5.bat ^>log5.log
        )

        echo Waiting for subprocesses to end
        break | >nul 2>nul (
            for /l %%a in (0) do @(ren "%%~ft" "%%~nxt" && exit || ping -n 2 "")
        )
        echo Done    

    ) & del "%%~ft"
```
这将生成一个临时文件，并通过创建到该文件的重定向来锁定它，并在此重定向中启动批处理子进程。 当所有子进程结束时，重定向将关闭，并且临时文件将被删除。

子进程运行时，文件被锁定，我们可以测试一下尝试重命名文件。 如果我们可以重命名文件，则子进程已经结束，否则某些进程仍在运行。

